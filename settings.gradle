pluginManagement {
	repositories {
		gradlePluginPortal()
		maven { url "https://repo.spring.io/plugins-release" }
	}
}

plugins {
	id "com.gradle.enterprise" version "3.7.2"
	id "io.spring.ge.conventions" version "0.0.7"
}

include "spring-aop"
include "spring-aspects"
include "spring-beans"
include "spring-context"
include "spring-context-support"
include "spring-core"
include "spring-expression"
include "spring-instrument"
include "spring-jcl"
include "spring-jdbc"
include "spring-orm"
include "spring-oxm"
include "spring-tx"
include "spring-web"
include "spring-webmvc"
include "framework-bom"
include 'spring-kevin'


rootProject.name = "spring"
rootProject.children.each {project ->
	project.buildFileName = "${project.name}.gradle"
}

settings.gradle.projectsLoaded {
	gradleEnterprise {
		buildScan {
			File buildDir = settings.gradle.rootProject.getBuildDir()
			buildDir.mkdirs()
			new File(buildDir, "build-scan-uri.txt").text = "(build scan not generated)"
			buildScanPublished { scan ->
				if (buildDir.exists()) {
					new File(buildDir, "build-scan-uri.txt").text = "${scan.buildScanUri}\n"
				}
			}
		}
	}
}

